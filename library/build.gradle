apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

afterEvaluate {
    apply from: 'gradle/publish.gradle'
}

/**
 * Holds the information associated with the library needed for the
 * bintray plugin to publish the artifact
 */
class LibraryInfo {
    static Integer versionMajor = 4
    static Integer versionMinor = 0
    static Integer versionPatch = 0

    static String artifactId = 'recyclerext'
    static String groupId = 'com.devbrackets.android'
    static String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    static Integer versionCode = versionMajor * 10_000 + versionMinor * 1_000 + versionPatch * 100
}

static def getLibraryInfo() {
    return new LibraryInfo()
}

dependencies {
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.recyclerview:recyclerview:1.1.0'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        versionCode getLibraryInfo().versionCode
        versionName getLibraryInfo().versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}